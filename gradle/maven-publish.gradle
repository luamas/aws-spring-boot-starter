apply plugin: 'maven-publish'
apply plugin: 'signing'


task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html4', true)
    }
}
if(hasProperty("sonatypeUsername")) {
    publishing {
        publications {
            maven(MavenPublication) {
                artifactId project.name
                from components.java
                artifact sourcesJar
                artifact javadocJar
                pom {
                    name = project.name
                    description = 'AWS spring boot starter'
                    url = "https://git.luamas.com/luamas/aws-spring-boot-starter"

                    scm {
                        connection = "scm:git:https://git.luamas.com/luamas/aws-spring-boot-starter"
                        developerConnection = "scm:git:https://git.luamas.com/luamas/aws-spring-boot-starter"
                        url = "https://git.luamas.com/luamas/aws-spring-boot-starter"
                    }

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = "luamas"
                            name = "luamas"
                            email = "myluamas@gmail.com"
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
    }
    signing {
        sign publishing.publications.maven
    }

}
